#!/usr/bin/env gosh
;; Generic application server script.
;;
;;  Copyright (c) 2003 Scheme Arts, L.L.C., All rights reserved.
;;  Copyright (c) 2003 Time Intermedia Corporation, All rights reserved.
;;  See COPYING for terms and conditions of using this software
;;
;; $Id: kahua-server.in,v 1.12 2004/05/07 02:47:30 nobsun Exp $

;; This script would be called with a name of the actual application server
;; module name.
;;
;;    kahua-server [-c <conf-file>] <app-server> <args> ...
;; 
;; <app-server> is a module name that implements the service, such as
;; kahua.app.standard.
;;
;; This server code doesn't depend much on kahua internals, to make it
;; to reload the server code without restarting.
;;

(define-module kahua-server
  (use gauche.logger)
  (use gauche.net)
  (use gauche.selector)
  (use gauche.parameter)
  (use gauche.collection)
  (use gauche.parseopt)
  (use file.util)
  (use srfi-1)
  (use kahua)
  (export primary-database-name
          main-proc
          kahua-error-proc
          kahua-app-args
          initialize-main-proc
          kahua-server-main)
  )
(select-module kahua-server)

(define primary-database-name (make-parameter #f))
(define kahua-app-args (make-parameter #f))

(define (kahua-application-environment)
  (let ((sandbox (make-sandbox-module))
        (cm (current-module)))
    (for-each (lambda (e)
                (eval `(define-in-module ,sandbox ,(car e) ,(cdr e))
                      cm))
              (map (lambda (s) (cons s (eval s cm))) (module-exports cm)))
    ;; define load-kahua-module as load in sandbox.
    (eval `(define-in-module ,sandbox load ,load-kahua-module) cm)
    sandbox))

;; set sandbox module in main proc.
(define kahua-app-server #f)

(define main-proc (make-parameter (lambda _
 				    (error "Not initialized!"))))
(define kahua-error-proc (make-parameter #f))
(define (initialize-main-proc proc)
  (main-proc proc))

(define (ping-request? header)
  (assoc "x-kahua-ping" header))

(define (handle-request header body reply-cont selector)
  (if (ping-request? header)
    (reply-cont #t #t)
    (let1 dbname (or (primary-database-name)
                     (build-path (ref (kahua-config) 'working-directory) "db"))
      (with-db (db dbname)
        (kahua-default-handler header body reply-cont default-handler
                               :error-proc (kahua-error-proc)
                               :eval-environment (current-module)))))
  )

(define (default-handler) ((main-proc)))

(define (load-kahua-module mod)
  (with-error-handler
      (lambda (e)
	(report-error e)
        (print "ERROR loading module")
        (exit 0))
    (lambda ()
      (load mod :environment kahua-app-server))))

(define (run-server worker-id sockaddr)
  (let ((sock (make-server-socket sockaddr :reuse-addr? #t))
        (selector (make <selector>)))
    (define (accept-handler fd flag)
      (let* ((client (socket-accept sock))
             (input  (socket-input-port client :buffered? #f))
             (output (socket-output-port client))
             (header (read input))
             (body   (read input))
             )
        (handle-request header body
                        (lambda (r-header r-body)
                          (write r-header output) (newline output)
                          (write r-body output)   (newline output)
                          (flush output)
                          (socket-close client))
                        selector)))

    ;; hack
    (when (is-a? sockaddr <sockaddr-un>)
      (sys-chmod (sockaddr-name sockaddr) #o777))

    (format #t "~a\n" worker-id)
    (selector-add! selector (socket-fd sock) accept-handler '(r))
    (do () (#f) (selector-select selector))
    ))

(define *kahua-top-module* #f)

;; update server with top-module or some files
(define (update-server . files)
  (define (load-k-module mod)
    (with-error-handler
     (lambda (e)
       (report-error e)
       #f)
     (lambda ()
       (load mod :environment kahua-app-server))))

  (if (pair? files)
      (every (lambda (f) (load-k-module f)) files)
      (load-k-module *kahua-top-module*)))

(define (kahua-server-main args)
  (let-args (cdr args) ((conf-file "c=s" #f)
                        (user "user=s" #f)
                        (keyserv "k=s" #f)
                        . mods)
    (unless (pair? mods)
      (error "usage: kahua-server [-c <conf>] [-user <user>] [-k <keyserv-id>] <app-server> <args> ..." mods))
    (set! *kahua-top-module* (car mods))
    (kahua-init conf-file :user user)
    (set! kahua-app-server (kahua-application-environment))
    (initialize-plugins)
    (kahua-app-args (cdr mods))
    (load-kahua-module (car mods))
    (let* ((worker-id (kahua-init-server 
		       (car (string-split (sys-basename (car mods)) #\.))
                       keyserv))
           (sockbase  (kahua-sockbase))
           (sockaddr  (worker-id->sockaddr worker-id sockbase))
           (cleanup   (lambda ()
                        (when (is-a? sockaddr <sockaddr-un>)
                          (sys-unlink (sockaddr-name sockaddr))))))
      (set-signal-handler! SIGINT  (lambda _ (cleanup) (exit 0)))
      (set-signal-handler! SIGHUP  (lambda _ (cleanup) (exit 0)))
      (set-signal-handler! SIGTERM (lambda _ (cleanup) (exit 0)))
      (with-error-handler
          (lambda (e)
            (report-error e)
            (cleanup)
            70)
        (lambda ()
          (run-server worker-id sockaddr))))
    ))

;; Main -----------------------------------------------------

(select-module user)
(import kahua-server)

(define (main args) (kahua-server-main args))

;; Local variables:
;; mode: scheme
;; end:
